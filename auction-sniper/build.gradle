plugins {
    id 'java-library'
    id 'eclipse'
}

repositories {
    jcenter()
}

sourceSets {
    acceptance {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    acceptanceImplementation.extendsFrom testImplementation
    acceptanceRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'org.igniterealtime.smack:smack:3.2.1'
    implementation 'org.igniterealtime.smack:smackx:3.2.1'
    implementation 'org.apache.commons:commons-lang3:3.9'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:3.1.0'

    acceptanceImplementation 'com.googlecode.windowlicker:windowlicker-swing:r268'
    acceptanceImplementation  'commons-io:commons-io:2.6'
}

def printTestResult = {desc, result ->
    if (!desc.parent) {
        println "Test result: ${result.resultType}"
        println "Test summary: ${result.testCount} tests, " +
                "${result.successfulTestCount} succeeded, " +
                "${result.failedTestCount} failed, " +
                "${result.skippedTestCount} skipped\n"
    }
}

test {
    useJUnitPlatform()
    afterSuite printTestResult
}

task acceptanceTest(type: Test) {
    description = 'Runs acceptance tests.'
    group = 'verification'
    testClassesDirs = sourceSets.acceptance.output.classesDirs
    classpath = sourceSets.acceptance.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()
    afterSuite printTestResult
}

check.dependsOn acceptanceTest

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
        plusConfigurations += [ configurations.acceptanceRuntimeClasspath ]
    }
}
